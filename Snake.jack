class Snake {
  function Array init(int l) {
    var Array snake;
    var int i;
    let snake = Array.new(l);   //создаём массив длиной l, который будет хранить блоки 
    let i = 0;
    while (i < l) {
      let snake[i] = Block.new(i, 0);   //заполняем массив блоками с координатами ({0<..<len(snake)}, 0)
      do Block.drawFullBlock(snake[i], true); 
      // do Block.printBlockCoord(snake[i]);
      let i = i + 1;
    }
    return snake;
  }
 
  function Block getBlock(Array snake, int indx) {  //возвращает один из блоков по индексу из змейки
    return snake[indx];
  }
 
  function void moveSnake(Array snake, int l, int dir) {  //dir == 1 вправо, dir == 2 влево, dir == 3 вниз, dir == 4 вверх
    var int i, tmpl;
    let i = 0;
    let tmpl = l - 1;
    do Block.drawFullBlock(snake[i], false);    //делаем белым первый блок змейки
    do Block.dispose(snake[i]);     //удаляем обесцвеченый ранее блок
    while (i < tmpl) {
      let snake[i] = snake[i + 1];  //переприсваиваем все средние блоки змейки не включая голову и хвост
      // do Block.printBlockCoord(snake[i]);
      let i = i + 1;
    }
    let snake[l - 1] = Block.moveHeadBlock(snake[l - 1], dir);    //отрисовывает голову змейки в соответствии с направлением
    // do Block.printBlockCoord(snake[l - 1]);
    return;
  }

  function Array lengthenSnake(Array snake, int l, Block target) {
    var int newLength, i;
    var Array newSnake;
    do Block.drawFullBlock(target, true); //рисуем новый квадрат
    let newLength = l + 1;
    let newSnake = Array.new(newLength);  //пересоздаём змейку с новой длиной
    let i = 0;
    while (i < l) {
      let newSnake[i] = snake[i];   //переприсваеваем блоки из старой змейки в новую
      // do Block.printBlockCoord(newSnake[i]);
      let i = i + 1;
    }
    do snake.dispose();   //удаляем старую змейку
    let newSnake[i] = target;
    return newSnake;
  } 

  function void dispose(Array snake, int l) {
    var int i;
    let i = 0;
    while (i < l) {
      do Block.dispose(Snake.getBlock(snake, i));   //убираем все блоки в змейке
      let i = i + 1;
    }
    do snake.dispose();   //и убираем элементы массива 
    return;
  }
  
  // debug
  function void printSnakeCoords(Array snake, int l) {  //для отладки
    var int i;
    let i = 0;
    while (i < l) {
      do Block.printBlockCoord(snake[i]);
      let i = i + 1;
    }
    return;
  }   
}